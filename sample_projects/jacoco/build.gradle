apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'gradle-execfork-plugin'

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath "io.spine.tools:gradle-execfork-plugin:$pluginVersion"
  }
}

task workerTask(dependsOn: 'startDaemon') {
  doLast {
    sleep(1000)
    assert file("$buildDir/daemon.log").text.contains("PING")
  }
}

task startDaemon(type: io.spine.tools.gradle.exec.JavaExecFork, dependsOn: 'classes') {
  classpath = sourceSets.main.runtimeClasspath
  main = 'io.spine.tools.example.Main'
  standardOutput = "$buildDir/daemon.log"
  waitForOutput = 'PING'
  stopAfter = workerTask
  jacoco.applyTo(it)
}

task daemonCodeCoverageReport(type: JacocoReport) {
  executionData startDaemon.jacoco.destinationFile
  classDirectories.setFrom(sourceSets.main.runtimeClasspath)
  sourceSets sourceSets.main
  reports.xml.enabled = true
  reports.html.enabled = false
}
daemonCodeCoverageReport.mustRunAfter stopDaemon
workerTask.finalizedBy daemonCodeCoverageReport


task verify(dependsOn: 'daemonCodeCoverageReport') {
  doLast {
    assert file("$buildDir/reports/jacoco/daemonCodeCoverageReport/daemonCodeCoverageReport.xml").text.contains('<line nr="5"')
  }
}

build.dependsOn workerTask
build.dependsOn verify
